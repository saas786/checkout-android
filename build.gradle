// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: 'version.gradle'

buildscript {
    repositories {
        google()
        // Required for Browserstack gradle plugin
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'gradle.plugin.com.browserstack.gradle:browserstack-gradle-plugin:3.0.3'
        classpath 'com.android.tools.build:gradle:7.0.4'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            // Uncomment to see details deprecation warnings
            //options.compilerArgs.add("-Xlint:deprecation")
        }
    }
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

task clean(type: Delete) {
    dependsOn(":checkout:clean")
    dependsOn(":example-checkout:clean")
    dependsOn(":example-shop:clean")
    dependsOn(":shared-test:clean")
}

ext {
    versionCode = getVersionCode()
    versionName = getVersionName()
    javaCompatVersion = JavaVersion.VERSION_1_8

    nexusUser = System.getenv('NEXUS_USER')
    nexusPassword = System.getenv('NEXUS_PASSWORD')
    packageCloudToken = System.getenv('PACKAGECLOUD_TOKEN')
    browserStackUser = System.getenv('BROWSERSTACK_USER')
    browserStackKey = System.getenv('BROWSERSTACK_KEY')

    merchantCode = System.getenv('MOBILE_MERCHANT_CODE')
    merchantPaymentToken = System.getenv('MOBILE_MERCHANT_PAYMENT_TOKEN')
    paymentApiListUrl = System.getenv('PAYMENTAPI_LISTURL')

    playstoreStoreFile = System.getenv('PLAYSTORE_UPLOAD_STOREFILE')
    playstoreStorePassword = System.getenv('PLAYSTORE_UPLOAD_STOREPASSWORD')
    playstoreKeyAlias = System.getenv('PLAYSTORE_UPLOAD_KEYALIAS')
    playstoreKeyPassword = System.getenv('PLAYSTORE_UPLOAD_KEYPASSWORD')

    minSdkVersion = 21
    compileSdkVersion = 31
    targetSdkVersion = 31
    lombokVersion = '1.18.22'

    gsonVersion = '2.8.9'
    androidMaterialVersion = '1.4.0'
    androidxBrowserVersion = '1.4.0'
    androidxSwiperefreshlayoutVersion = '1.1.0'
    androidxTestCoreVersion = '1.4.0'
    androidxTestRunnerVersion = '1.4.0'
    androidxTestRulesVersion = '1.4.0'
    androidxTestEspressoVersion = '3.4.0'
    androidxTestJunitVersion = '1.1.3'
    androidxTestUIAutomatorVersion = '2.2.0'

    junitVersion = '4.13.2'
    rxjavaVersion = '3.1.3'
    rxandroidVersion = '3.0.0'
    mockitoCoreVersion = '2.23.4'
    robolectricVersion = '4.7.3'
    jsonsnapshotVersion = '1.0.17'
    tngDataProviderVersion = '2.8'
}

static def getBranchName() {
    def branch = ''
    def proc = 'git rev-parse --abbrev-ref HEAD'.execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

static def getBranchLabel() {
    return getBranchName().replace('/', '-')
}

apply from: 'ci.gradle'
